plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}


apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

android {
    namespace 'org.nativescript.node_compat'
    compileSdk 33

    ndkVersion "22.1.7171670"

    defaultConfig {
        minSdk 17
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

cargo {
  profile = 'release'
  rustupChannel = "nightly"
  module  = "../../../../../crates/platforms/android"
  libname = "nodeandroid"
  targets = ["arm","arm64" ,"x86","x86_64"]
 // targets = ["x86_64"]
  targetDirectory = '../../../../../target'

  apiLevels = [
    "arm": 17,
    "arm64": 21,
    "x86": 17,
    "x86_64": 21
  ]

  extraCargoBuildArguments = ['-Z', 'build-std=std,panic_abort', '-Z', 'build-std-features=panic_immediate_abort']
}


tasks.whenTaskAdded { task ->
  if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
    task.dependsOn 'cargoBuild'
  }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
